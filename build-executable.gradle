apply plugin: 'application'

// Wire stdin to our run task's System.in
run {
    standardInput = System.in
}

ext {
    mainClassName = 'te.audio.m2sm.Startup'
    appName = 'm2sm'
    deployDir = System.getenv('SYSTEM_TOOLS')
}


jar {
    archiveName "${appName}.jar"
    from files(sourceSets.main.output.classesDir as Object)
    from files(sourceSets.main.output.resourcesDir as Object)
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task exe(type: Exec, dependsOn: [':jar']) {
    final jarFile = new File(buildDir, "libs/${appName}.jar")
    commandLine "g2exe", "--fileToConvert", /"${jarFile}"/
}

task deploy(type: Copy, dependsOn: [':exe']) {
    description "Build ${appName}.exe and move it to system tools"
    doFirst {
        println("Deploying ${appName}.exe to '${deployDir}'")
    }

    from "${buildDir}/libs/${appName}.exe"
    into deployDir
}